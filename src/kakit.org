#+TITLE: Kakit
#+AUTHOR: Abhishek Kumar
#+EMAIL: abhi.kr.2100@gmail.com
#+LANGUAGE: en
#+PROPERTY: header-args :results silent :mkdirp yes

This file is the entrypoint to the Kakit package.

#+begin_src emacs-lisp :tangle kakit/kakit.el
;;; kakit.el --- A Kakoune-inspired editing experience for Emacs -*- lexical-binding: t; -*-

;; Copyright (C) 2025 Abhishek Kumar

;; Author: Abhishek Kumar <abhi.kr.2100@gmail.com>
;; Version: 1.0.0
;; Package-Requires: ((emacs "30.1"))
;; URL: https://github.com/abhi-kr-2100/Kakit

;;; Commentary:

;; Kakit is a Kakoune-inspired editing experience for Emacs.

;;; Code:

(require 'kakit-states-state-switching-helper)
(require 'kakit-modes-normal)
(require 'kakit-keymaps-normal)
(require 'kakit-keymaps-insert)
(require 'kakit-keymaps-command-pending)

(add-to-list 'emulation-mode-map-alists
             `((kakit-normal-mode . ,kakit-normal-state-keymap)))

(add-to-list 'emulation-mode-map-alists
             `((kakit-insert-mode . ,kakit-insert-state-keymap)))

(add-to-list 'emulation-mode-map-alists
             `((kakit-command-pending-mode . ,kakit-command-pending-state-keymap)))

(defun kakit--get-default-state ()
  "Return the default state for the current buffer."
  (if (minibufferp)
      'insert
    'normal))

(defun kakit--init-cleanup-helper ()
  "Turn on Kakit mode and clean up on turn off."
  (if kakit-mode
      (kakit--switch-state-local (kakit--get-default-state))
    (dolist (state-mode kakit--states)
      (let ((state (car state-mode))
            (mode (cdr state-mode)))
        (when (eq state kakit--current-state-local)
          (setq-local kakit--current-state-local nil)
          (funcall mode -1))))))

;;;###autoload
(define-globalized-minor-mode kakit-mode
  kakit-normal-mode
  (lambda () (kakit--switch-state-local (kakit--get-default-state)))
  :predicate t
  (kakit--init-cleanup-helper))

(provide 'kakit)

;;; kakit.el ends here
#+end_src
