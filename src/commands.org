#+TITLE: Commands
#+AUTHOR: Abhishek Kumar
#+EMAIL: abhi.kr.2100@gmail.com
#+LANGUAGE: en
#+PROPERTY: header-args :results silent :mkdirp yes

While Emacs has many of Kakoune's commands built-in, some
commands still need to be implemented. Such commands are
implemented in this file.

* State Switching Commands

The state switching commands primarily switch from one Kakit
state to another. Some have side-effects, such as deleting
the selected region before switching to the insert state
from the normal state.

** Purely State Switching Commands

#+begin_src emacs-lisp :tangle kakit/kakit-commands-purely-state-switching.el
; -*- lexical-binding: t -*-

(require 'kakit-states-state-switching-helper)

(defun kakit-switch-to-normal-mode-local ()
  "Switch to Kakit's Normal state.
Disable any other Kakit minor mode."
  (interactive)
  (kakit--switch-state-local 'normal))

(defun kakit-switch-to-insert-mode-local ()
  "Switch to Kakit's Insert state.
Disable any other Kakit minor mode."
  (interactive)
  (kakit--switch-state-local 'insert))

(provide 'kakit-commands-purely-state-switching)
#+end_src

** Switch to Insert Mode

For convenience, modal editors provide various ways to get
into the editing/inserting state: some position the cursor
at the right place, others indent into a block.

#+begin_src emacs-lisp :tangle kakit/kakit-commands-switch-to-insert-mode.el
; -*- lexical-binding: t -*-

(require 'kakit-commands-purely-state-switching)

(defun kakit-insert-after-char ()
  "Move cursor forward one character and switch to insert mode.
If at end of line, do not move."
  (interactive)
  (unless (eolp)
    (forward-char 1))
  (kakit-switch-to-insert-mode-local))

(defun kakit-insert-at-line-end ()
  "Move to the end of a line and switch to insert mode."
  (interactive)
  (end-of-line)
  (kakit-switch-to-insert-mode-local))

(defun kakit-insert-at-line-beginning ()
  "Move to the beginning of a line and switch to insert mode."
  (interactive)
  (beginning-of-line)
  (kakit-switch-to-insert-mode-local))

(defun kakit-open-newline-below ()
  "Insert a newline after the current line and switch to insert mode."
  (interactive)
  (end-of-line)
  (newline-and-indent)
  (kakit-switch-to-insert-mode-local))

(defun kakit-open-newline-above ()
  "Insert a newline before the current line and switch to insert mode."
  (interactive)
  (beginning-of-line)
  (newline)
  (previous-line)
  (indent-according-to-mode)
  (kakit-switch-to-insert-mode-local))

(provide 'kakit-commands-switch-to-insert-mode)
#+end_src

** Switching to Command-Pending State

Some operations switch from normal state to the insert
state, make an edit, and then switch back to the normal
state.

Such operations are modeled by Kakit using the
Command-Pending state.

A complete command consists of a prefix key which determines
the operation to perform followed by an argument for the
operation.

For example, consider replacing the character at point with
another character. The prefix will be 'r (for replace) and
the argument will be the character that should be inserted
in the place of the character that is present at point.

From normal mode, command-pending mode is entered into
always with a prefix.

#+begin_src emacs-lisp :tangle kakit/kakit-commands-state-switch-to-command-pending-state.el
; -*- lexical-binding: t -*-

(require 'kakit-states-state-switching-helper)
(require 'kakit-modes-command-pending-prefix-key)

(defun kakit--switch-to-command-pending-mode-with-prefix (prefix-key)
  "Switch to command-pending mode and set PREFIX-KEY."
  (setq-local kakit--command-pending-prefix-key prefix-key)
  (kakit--switch-state-local 'command-pending))

(defun kakit-switch-to-command-pending-mode-for-replace ()
  "Enter command-pending mode with 'r' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'r))

(defun kakit-switch-to-command-pending-mode-for-goto ()
  "Enter command-pending mode with 'g' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'g))

(defun kakit-switch-to-command-pending-mode-for-find ()
  "Enter command-pending mode with 'f' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'f))

(defun kakit-switch-to-command-pending-mode-for-find-backward ()
  "Enter command-pending mode with 'F' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'F))

(defun kakit-switch-to-command-pending-mode-for-till ()
  "Enter command-pending mode with 't' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 't))

(defun kakit-switch-to-command-pending-mode-for-till-backward ()
  "Enter command-pending mode with 'T' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'T))

(provide 'kakit-commands-state-switch-to-command-pending-state)
#+end_src

** Handling Prefix Argument

Commands may be passed a prefix argument which modifies the
effect they have. Prefix arguments are always a positive
integer.

Additionally, to keep things simple, for now, prefix
arguments are recorded only in the normal state.

#+begin_src emacs-lisp :tangle kakit/kakit-commands-handle-prefix-arg.el
; -*- lexical-binding: t -*-

(require 'kakit-modes-normal-prefix-arg)

(defun kakit-handle-digit-command (digit)
  "Handle digit input in normal mode to build prefix argument.
DIGIT is the numeric value of the pressed digit (0-9)."
  (interactive)
  (setq-local kakit--prefix-argument
        (if kakit--prefix-argument
            (+ (* kakit--prefix-argument 10) digit)
          digit)))

(defun kakit-reset-prefix-argument ()
  "Reset the prefix argument to nil."
  (interactive)
  (setq-local kakit--prefix-argument nil))

(provide 'kakit-commands-handle-prefix-arg)
#+end_src

* Movement Commands

Kakit provides all the usual movement commands but with a
twist: some of these select a region. If a region is already
present, they expand it. This behavior is different from Vim
as well as Emacs.

Separate commands are not required for hjkl motions because
Emacs already has suitable implementations for those.

** Word Based Motions

#+begin_src emacs-lisp :tangle kakit/kakit-commands-movement-word-based.el
;; -*- lexical-binding: t -*-

(defun kakit-forward-word-select (&optional count)
  "Move forward COUNT words and select the region traversed.
If COUNT is nil, move forward one word."
  (interactive "P")
  (unless (or (eobp) (use-region-p))
    (push-mark (point) t t))
  (forward-word count))

(defun kakit-backward-word-select (&optional count)
  "Move backward COUNT words and select the region traversed.
If COUNT is nil, move backward one word."
  (interactive "P")
  (unless (or (bobp) (use-region-p))
    (push-mark (point) t t))
  (backward-word count))

(defun kakit-word-end-select (&optional count)
  "Move point to the end of COUNT words from point and select the region traversed.
If COUNT is nil, move to the end of current word."
  (interactive "P")
  (kakit-forward-word-select count))

(provide 'kakit-commands-movement-word-based)
#+end_src

** Line Based Motions

#+begin_src emacs-lisp :tangle kakit/kakit-commands-movement-line-based.el
; -*- lexical-binding: t -*-

(defun kakit-select-line-and-next (&optional count)
  "Select COUNT lines starting from the current line and move to the next line.
If COUNT is nil, select one line.

If a region is active, extend it. If on the last line, just selects
the line and point stays at the end."
  (interactive "P")
  (unless (use-region-p)
    (beginning-of-line)
    (set-mark-command nil))
  (next-line count))

(provide 'kakit-commands-movement-line-based)
#+end_src

** Command-Pending Based Motions

It's popular in Vim and Kakoune to quickly move to positions
in a file based on the ~g~ command. Moving to or till a
character is also very popular.

#+begin_src emacs-lisp :tangle kakit/kakit-commands-movement-command-pending.el
; -*- lexical-binding: t -*-

(require 'kakit-modes-command-pending-prefix-key)
(require 'kakit-commands-purely-state-switching)

(defun kakit--goto-command (char)
  "Execute goto command based on CHAR.
If CHAR is:
- 'e' - go to end of buffer
- 'g' - go to start of buffer
- 'h' - go to start of current line
- 'l' - go to end of current line"
  (pcase char
    (?e (goto-char (point-max)))
    (?g (goto-char (point-min)))
    (?h (beginning-of-line))
    (?l (end-of-line))))

(defun kakit--find-char (char &optional backward include-char count)
  "Move point to next occurrence of CHAR.
If BACKWARD is non-nil, search backward.
If INCLUDE-CHAR is non-nil, include the character in selection.
If COUNT is non-nil, repeat the search COUNT times.
Search is case-sensitive."
  (let* ((to-find (char-to-string char))
         (start-pos (point))
         (case-fold-search nil)
         (search-fn (if backward 'search-backward 'search-forward))
         (found-pos (funcall search-fn to-find nil t count)))
    (when found-pos
      (unless include-char
        (if backward
            (forward-char 1)
          (backward-char 1)))
      (unless (use-region-p)
        (push-mark start-pos t t)))))

(defun kakit-command-pending-act (&optional count)
  "Act based on `kakit--command-pending-prefix-key'.
If COUNT is non-nil, it'll used if required by the underlying command."
  (interactive "P")
  (when (characterp last-command-event)
    (let ((char last-command-event)
	  (prefix kakit--command-pending-prefix-key))
      (pcase prefix
	('r (kakit--replace-region-or-char-with char))
	('g (kakit--goto-command char))
	('f (kakit--find-char char nil t count))
	('F (kakit--find-char char t t count))
	('t (kakit--find-char char nil nil count))
	('T (kakit--find-char char t nil count)))))
  (kakit-switch-to-normal-mode-local))

(provide 'kakit-commands-movement-command-pending)
#+end_src

* Text Manipulation Commands

The primary purpose of the commands below is to modify text.

#+begin_src emacs-lisp :tangle kakit/kakit-commands-text-manipulation.el
;; -*- lexical-binding: t -*-

(require 'kakit-commands-purely-state-switching)

(defun kakit-kill-region-or-char (&optional count)
  "If region is active, kill it. Otherwise, delete COUNT chars at point.
If COUNT is nil, delete one char."
  (interactive "P")
  (if (use-region-p)
      (kill-region (region-beginning) (region-end))
    (delete-char (or count 1))))

(defun kakit-join-lines (&optional count)
  "Join COUNT lines starting from the current line.
If COUNT is nil, join the current line with the next line.

Does nothing if the current line is the last line in the buffer."
  (interactive "P")
  (let ((count (or count 1)))
    (dotimes (_ count)
      (join-line t))))

(defun kakit-change (&optional count)
  "Delete the selected region or COUNT chars at point, then switch to insert mode.
If COUNT is nil, delete one character."
  (interactive "P")
  (kakit-kill-region-or-char count)
  (kakit-switch-to-insert-mode-local))

(defun kakit--replace-region-or-char-with (char)
  "Replace selection or char at point with the CHAR.
If a region is active, replace the entire region with CHAR
repeated to match the region's length."
  (if (use-region-p)
      (let* ((beg (region-beginning))
             (end (region-end))
             (len (- end beg)))
        (delete-region beg end)
        (insert-char char len))
    (progn
      (kakit-kill-region-or-char)
      (insert-char char 1))))

(provide 'kakit-commands-text-manipulation)
#+end_src
