#+TITLE: Commands
#+AUTHOR: Abhishek Kumar
#+EMAIL: abhi.kr.2100@gmail.com
#+LANGUAGE: en
#+PROPERTY: header-args :results silent

While Emacs has many of Kakoune's commands built-in, some commands
still need to be implemented. Such commands are implemented in this
file.

* State Switching Commands

#+begin_src emacs-lisp :tangle kakit/kakit-commands-state-switching.el :mkdirp yes
; -*- lexical-binding: t -*-

(defun kakit-switch-to-normal-mode-local ()
  "Switch to Kakit's Normal state.
Disable any other Kakit minor mode."
  (interactive)
  (kakit--switch-state-local 'normal))

(defun kakit-switch-to-insert-mode-local ()
  "Switch to Kakit's Insert state.
Disable any other Kakit minor mode."
  (interactive)
  (kakit--switch-state-local 'insert))

(defun kakit-insert-after-char ()
  "Move cursor forward one character and switch to insert mode.
If at end of line, do not move."
  (interactive)
  (unless (eolp)
    (forward-char 1))
  (kakit-switch-to-insert-mode-local))

(defun kakit-insert-at-line-end ()
  "Move to the end of a line and switch to insert mode."
  (interactive)
  (end-of-line)
  (kakit-switch-to-insert-mode-local))

(defun kakit-insert-at-line-beginning ()
  "Move to the beginning of a line and switch to insert mode."
  (interactive)
  (beginning-of-line)
  (kakit-switch-to-insert-mode-local))

(defun kakit-open-newline-below ()
  "Insert a newline after the current line and switch to insert mode."
  (interactive)
  (end-of-line)
  (newline-and-indent)
  (kakit-switch-to-insert-mode-local))

(defun kakit-open-newline-above ()
  "Insert a newline before the current line and switch to insert mode."
  (interactive)
  (beginning-of-line)
  (newline)
  (previous-line)
  (indent-according-to-mode)
  (kakit-switch-to-insert-mode-local))

(defun kakit--switch-to-command-pending-mode-with-prefix (prefix-key)
  "Switch to command-pending mode and set PREFIX-KEY."
  (setq-local kakit--command-pending-prefix-key prefix-key)
  (kakit--switch-state-local 'command-pending))

(defun kakit-switch-to-command-pending-mode-for-replace ()
  "Enter command-pending mode with 'r' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'r))

(defun kakit-switch-to-command-pending-mode-for-goto ()
  "Enter command-pending mode with 'g' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'g))

(defun kakit-switch-to-command-pending-mode-for-find ()
  "Enter command-pending mode with 'f' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'f))

(defun kakit-switch-to-command-pending-mode-for-find-backward ()
  "Enter command-pending mode with 'F' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'F))

(defun kakit-switch-to-command-pending-mode-for-till ()
  "Enter command-pending mode with 't' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 't))

(defun kakit-switch-to-command-pending-mode-for-till-backward ()
  "Enter command-pending mode with 'T' as the prefix key."
  (interactive)
  (kakit--switch-to-command-pending-mode-with-prefix 'T))

(provide 'kakit-commands-state-switching)
#+end_src

* Movement Commands

#+begin_src emacs-lisp :tangle kakit/kakit-commands-movement.el :mkdirp yes
;; -*- lexical-binding: t -*-

(defun kakit-forward-word-select ()
  "Move forward one word and select the region traversed."
  (interactive)
  (unless (eobp)
    (unless (use-region-p)
      (push-mark (point) t t))
    (forward-word)))

(defun kakit-backward-word-select ()
  "Move backward one word and select the region traversed."
  (interactive)
  (unless (bobp)
    (unless (use-region-p)
      (push-mark (point) t t))
    (backward-word)))

(defun kakit-word-end-select ()
  "Move point to the end of the word at point and select the region traversed."
  (interactive)
  (kakit-forward-word-select))

(defun kakit-select-line-and-next ()
  "Select the current line and move to the next line.

If a region is active, extend it. If on the last line, just selects
the line and point stays at the end."
  (interactive)
  (unless (use-region-p)
    (beginning-of-line)
    (set-mark-command nil))
  (end-of-line)
  (unless (eobp)
    (next-line 1)
    (beginning-of-line)))

(provide 'kakit-commands-movement)
#+end_src

* Text Manipulation Commands

#+begin_src emacs-lisp :tangle kakit/kakit-commands-text-manipulation.el :mkdirp yes
;; -*- lexical-binding: t; -*-

(defun kakit-kill-region-or-char ()
  "If region is active, kill it. Otherwise, delete char at point."
  (interactive)
  (if (use-region-p)
      (kill-region (region-beginning) (region-end))
    (delete-char 1)))

(defun kakit-join-lines ()
  "Move to the next line and join it with the previous line.

Does nothing if the current line is the last line in the buffer."
  (interactive)
  (join-line -1))

(defun kakit-change ()
  "Delete the selected region or the character at point, then switch to insert mode."
  (interactive)
  (kakit-kill-region-or-char)
  (kakit-switch-to-insert-mode-local))

(defun kakit--replace-region-or-char-with (char)
  "Replace selection or char at point with the CHAR.
If a region is active, replace the entire region with CHAR
repeated to match the region's length."
  (if (use-region-p)
      (let* ((beg (region-beginning))
             (end (region-end))
             (len (- end beg)))
        (delete-region beg end)
        (insert-char char len))
    (progn
      (kakit-kill-region-or-char)
      (insert-char char 1))))

(defun kakit--goto-command (char)
  "Execute goto command based on CHAR.
If CHAR is:
- 'e' - go to end of buffer
- 'g' - go to start of buffer
- 'h' - go to start of current line
- 'l' - go to end of current line"
  (pcase char
    (?e (goto-char (point-max)))
    (?g (goto-char (point-min)))
    (?h (beginning-of-line))
    (?l (end-of-line))))

(defun kakit--find-char (char &optional backward include-char)
  "Move point to next occurrence of CHAR.
If BACKWARD is non-nil, search backward.
If INCLUDE-CHAR is non-nil, include the character in selection.
Search is case-sensitive."
  (let* ((to-find (char-to-string char))
         (start-pos (point))
         (case-fold-search nil)
         (found-pos (if backward
                       (search-backward to-find nil t)
                     (search-forward to-find nil t))))
    (when found-pos
      (unless include-char
        (if backward
            (forward-char 1)
          (backward-char 1)))
      (unless (use-region-p)
        (push-mark start-pos t t)))))

(defun kakit-command-pending-act ()
  "Act based on `kakit--command-pending-prefix-key'."
  (interactive)
  (when (characterp last-command-event)
    (let ((char last-command-event)
          (prefix kakit--command-pending-prefix-key))
      (pcase prefix
        ('r (kakit--replace-region-or-char-with char))
        ('g (kakit--goto-command char))
        ('f (kakit--find-char char nil t))
        ('F (kakit--find-char char t t))
        ('t (kakit--find-char char))
        ('T (kakit--find-char char t nil))
        (_ nil))))
  (kakit-switch-to-normal-mode-local))

(provide 'kakit-commands-text-manipulation)
#+end_src
